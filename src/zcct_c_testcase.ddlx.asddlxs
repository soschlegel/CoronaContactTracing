@Metadata.layer: #CUSTOMER
@Search.searchable: true
annotate view ZCCT_C_TESTCASE with
{
  @UI.facet: [ { id:              'testcase',
                   purpose:         #STANDARD,
                   type:            #IDENTIFICATION_REFERENCE,
                   label:           'Testcase - Details',
                   position:        10 } ]
  @UI.hidden: true
  testid;

  @UI: {
      lineItem:       [ { position: 10, importance: #HIGH, label: 'Testperson' } ],
      identification: [ { position: 10, label: 'Testperson' } ]}
  @Consumption.valueHelpDefinition: [{ entity : {name: 'ZCCT_I_TEST_PERSON', element: 'test_person_id'  } }]
  @Search.defaultSearchElement: true
  personid;

  @UI: {
      lineItem:       [ { position: 20, importance: #HIGH , label: 'Mitarbeiter'} ],
      identification: [ { position: 20, label: 'Mitarbeiter' } ] }
  @Consumption.valueHelpDefinition: [{ entity : {name: 'ZCCT_I_HealthDepEm', element: 'emplyee_id'  } }]
  @Search.defaultSearchElement: true
  employeeid;

  @UI: {
      lineItem:       [ { position: 30, importance: #HIGH, label: 'Testart'} ],
      identification: [ { position: 30, label: 'Testart' } ] }
  @Consumption.valueHelpDefinition: [{ entity : {name: 'ZCCT_I_TESTTYPE', element: 'testtypeid'  } }]
  @Search.defaultSearchElement: true
  testtype;

  @UI: {
      lineItem:       [ { position: 40, importance: #HIGH, label: 'Testdatum'} ],
      identification: [ { position: 40, label: 'Testdatum' } ] }
  @Search.defaultSearchElement: true
  testdate;

  @UI: {
      lineItem:       [ { position: 50, importance: #HIGH, label: 'Testzeit' } ],
      identification: [ { position: 50, label: 'Testzeit' } ] }
  @Search.defaultSearchElement: true
  testtime;

  @UI: {
      lineItem:       [ { position: 100, importance: #HIGH, label: 'Testergebnis', criticality: 'testresultcolor' },
                        { label: 'ðŸŸ¢ Negativ', type: #FOR_ACTION, dataAction: 'setNegative' },
                        { label: 'ðŸ”´ Positiv', type: #FOR_ACTION, dataAction: 'setPositive' } ],
      identification: [ { position: 35, label: 'Testergebnis', criticality: 'testresultcolor' },
                        { label: 'ðŸŸ¢ Negativ', type: #FOR_ACTION, dataAction: 'setNegative' },
                        { label: 'ðŸ”´ Positiv', type: #FOR_ACTION, dataAction: 'setPositive' } ] }
  @Consumption.valueHelpDefinition: [{ entity : {name: 'ZCCT_I_TESTRESULT', element: 'testresultid'  } }]
  @Search.defaultSearchElement: true
  testresult;

  @UI: {
      lineItem:       [ { position: 70, importance: #HIGH, label: 'Testergebnisdatum' } ],
      identification: [ { position: 70, label: 'Testergebnisdatum' } ] }
  @Search.defaultSearchElement: true
  testresultdate;

  @UI: {
      lineItem:       [ { position: 80, importance: #HIGH, label: 'Testergebniszeit' } ],
      identification: [ { position: 80, label: 'Testergebniszeit' } ] }
  @Search.defaultSearchElement: true
  testresulttime;

  @UI.hidden: true
  created_by;
  @UI.hidden: true
  created_at;
  @UI.hidden: true
  last_changed_by;
  @UI.hidden: true
  last_changed_at;
  @UI.hidden: true
  local_last_changed_at;
}